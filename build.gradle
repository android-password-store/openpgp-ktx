/*
 * Copyright Â© 2019 The Android Password Authors. All Rights Reserved.
 * SPDX-License-Identifier: GPL-3.0-Only
 */
buildscript {
    apply from: './dependencies.gradle'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath deps.gradle_plugin.android
        classpath deps.gradle_plugin.kotlin
    }
}

subprojects {
    apply from: rootProject.file('dependencies.gradle')
    repositories {
        google()
        jcenter()
    }
    if (plugins.hasPlugin('kotlin-android')) {
        dependencies {
            implementation deps.kotlin.stdlib8
        }
    }
    if (it.name == 'sample') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    android {
        compileSdkVersion versions.compileSdk
        buildToolsVersion = versions.buildTools
        defaultConfig {
            minSdkVersion versions.minSdk
            targetSdkVersion versions.targetSdk
            versionCode versions.versionCode
            versionName versions.versionName
        }
        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
        sourceSets {
            main {
                java {
                    srcDirs 'src/main/kotlin'
                }
            }
        }
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }
}

tasks.wrapper {
    gradleVersion = "6.5.1"
    distributionType = Wrapper.DistributionType.ALL
    distributionSha256Sum = "143a28f54f1ae93ef4f72d862dbc3c438050d81bb45b4601eb7076e998362920"
}
