/*
 * Copyright Â© 2019 The Android Password Authors. All Rights Reserved.
 * SPDX-License-Identifier: GPL-3.0-Only
 */
buildscript {
    apply from: './dependencies.gradle'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath deps.gradle_plugin.android
        classpath deps.gradle_plugin.binary_compatibility_validator
        classpath deps.gradle_plugin.kotlin
    }
}

apply plugin: 'binary-compatibility-validator'

apiValidation {
    ignoredProjects += ["sample"]
}

subprojects {
    apply from: rootProject.file('dependencies.gradle')
    repositories {
        google()
        jcenter()
    }
    if (it.name == 'sample') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    android {
        compileSdkVersion versions.compileSdk
        defaultConfig {
            minSdkVersion versions.minSdk
            targetSdkVersion versions.targetSdk
            versionCode versions.versionCode
            versionName versions.versionName
        }
        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
        sourceSets.main.java.srcDirs 'src/main/kotlin'
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }
}

tasks.wrapper {
    gradleVersion = "6.7"
    distributionType = Wrapper.DistributionType.ALL
    distributionSha256Sum = "0080de8491f0918e4f529a6db6820fa0b9e818ee2386117f4394f95feb1d5583"
}
