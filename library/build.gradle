/*
 * Copyright Â© 2019 The Android Password Authors. All Rights Reserved.
 * SPDX-License-Identifier: GPL-3.0-Only
 */
plugins {
    id 'kotlin-android'
}

final def today = { ->
    final def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'date', '+%Y%m%d'
        standardOutput = stdout
    }
    stdout.toString().trim()
}

android {
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'MissingTranslation' // Can't do anything about this
    }

    libraryVariants.all {
        it.generateBuildConfigProvider.configure { enabled = false }
    }

    defaultConfig {
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/android-password-store/openpgp-ktx')
            credentials {
                username = project.findProperty('gpr.user') ?: System.getenv('USERNAME')
                password = project.findProperty('gpr.key') ?: System.getenv('PASSWORD')
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.release
            groupId "$GROUP"
            artifactId 'openpgp-ktx'
            version System.env['SNAPSHOT'] == null ? "$VERSION_NAME" : "$VERSION_NAME-${today()}"
        }
    }
}

dependencies {
    api deps.androidx.annotation
    api deps.androidx.appcompat
    api deps.androidx.preference
    api deps.kotlin.coroutines.core
    api deps.kotlin.coroutines.android
}
