/*
 * Copyright Â© 2019 The Android Password Authors. All Rights Reserved.
 * SPDX-License-Identifier: GPL-3.0-Only
 */
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'digital.wup.android-maven-publish' version '3.6.3'
}

final def gitHash = { ->
    final def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    stdout.toString().trim()
}

android {
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'MissingTranslation' // Can't do anything about this
    }

    libraryVariants.all {
        it.generateBuildConfigProvider.configure { enabled = false }
    }
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/android-password-store/openpgp-ktx')
            credentials {
                username = project.findProperty('gpr.user') ?: System.getenv('USERNAME')
                password = project.findProperty('gpr.key') ?: System.getenv('PASSWORD')
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.android
            groupId "$GROUP"
            artifactId 'openpgp-ktx'
            version System.env['GITHUB_WORKFLOW'] != null ? "$VERSION_NAME" : "${gitHash()}-SNAPSHOT"
        }
    }
}

dependencies {
    api 'androidx.annotation:annotation:' + versions.annotation
    api 'androidx.appcompat:appcompat:' + versions.appcompat
    api 'androidx.core:core-ktx:' + versions.coreKtx
    api 'androidx.preference:preference:' + versions.preference
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:' + versions.coroutines
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:' + versions.coroutines
}
