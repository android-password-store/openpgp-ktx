/*
 * Copyright Â© 2019 The Android Password Authors. All Rights Reserved.
 * SPDX-License-Identifier: GPL-3.0-Only
 */
plugins {
    id 'kotlin-android'
    id 'maven-publish'
}

final def gitHash = { ->
    final def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    stdout.toString().trim()
}

android {
    buildFeatures.buildConfig = false

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'MissingTranslation' // Can't do anything about this
    }

    defaultConfig {
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }

    kotlin.explicitApi = 'strict'

    kotlinOptions {
      freeCompilerArgs += [
          '-Xexplicit-api=strict',
      ]
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = 'GitHubPackages'
                url = uri('https://maven.pkg.github.com/android-password-store/openpgp-ktx')
                credentials {
                    username = project.findProperty('gpr.user') ?: System.getenv('USERNAME')
                    password = project.findProperty('gpr.key') ?: System.getenv('PASSWORD')
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from components.release
                groupId "$GROUP"
                artifactId 'openpgp-ktx'
                version System.env['SNAPSHOT'] == null ? "$VERSION_NAME" : "${gitHash()}"
            }
        }
    }
}

dependencies {
    api deps.androidx.annotation
    api deps.androidx.appcompat
    api deps.androidx.preference
    api deps.kotlin.coroutines.core
    api deps.kotlin.coroutines.android
}
